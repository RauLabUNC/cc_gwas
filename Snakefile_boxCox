# --- Configuration ---
QTL_TRAITS = [
    "BW.day.0", "BW.day.21", "TH", "LV", "RV", "LA", "RA", "Lung", "Liver", "Adrenal",
    "THW.by.BW.0", "LVW.by.BW.0", "RVW.by.BW.0", "LAW.by.BW.0", "RAW.by.BW.0", 
    "LuW.by.BW.0", "LiW.by.BW.0", "AdrW.by.BW.0", "IVSd.0", "LVIDd.0", "LVPWd.0", 
    "IVSs.0", "LVIDs.0", "LVPWs.0", "HR.0", "EF.0", "FS.0", "LV.Mass.0", 
    "LV.Mass.Corrected.0", "LV.Vold.0", "LV.Vols.0", "LVIDd.by.LVIDs.0", 
    "LVIDd.by.LVIDs.21", "IVSd.21", "LVIDd.21", "LVPWd.21", "IVSs.21", 
    "LVIDs.21", "LVPWs.21", "HR.21", "EF.21", "FS.21", "LV.Mass.21", 
    "LV.Mass.Corrected.21", "LV.Vold.21", "LV.Vols.21", "delta.EF", 
    "Wall.Thicknessd.0", "Wall.Thicknessd.21", "Delta.Wall.Thickness.d", 
    "Delta.LVIDd", "Delta.FS", "Percent.Fibrosis", "CSA"
]
NORMS = ["boxcox"]
AGGS = ["individual"]
DRUGS = ["Ctrl", "Iso"]
PHENO_INPUT = "data/raw/phenotypes/CC_Phenotypes_05022025.csv"
R_SCRIPT_PATH = "scripts/restructuring/normalizeTraits.R"

# --- Target Rule ---
rule all:
    input:
        expand("data/processed/scan_thresholds/{norm}_{agg}_{qtl_trait}_{drug}_perm.rds", 
                norm=NORMS, agg=AGGS, qtl_trait=QTL_TRAITS, drug=DRUGS)

# --- Rule: Preprocess Phenotypes ---
rule preprocess_phenotypes:
    input:
        script=R_SCRIPT_PATH,
        pheno=PHENO_INPUT
    output:
        processed_pheno="data/processed/phenotypes/boxCoxTest/{norm}_{agg}_{drug}.csv"
    params:
        norm=lambda wildcards: wildcards.norm,
        agg=lambda wildcards: wildcards.agg,
        drug=lambda wildcards: wildcards.drug
    shell:
        """
        Rscript {input.script} \
            --input {input.pheno} \
            --output {output.processed_pheno} \
            --normalization {params.norm} \
            --aggregation {params.agg} \
            --drug {params.drug}
        """

# --- Rule: Run RopScan ---
rule run_ropscan:
    input:
        script="scripts/restructuring/ropScan.R",
        processed_pheno="data/processed/phenotypes/boxCoxTest/{norm}_{agg}_{drug}.csv"
    output:
        scan="data/processed/ropscan/{norm}_{agg}_{qtl_trait}_{drug}.rds"
    params:
        norm=lambda wildcards: wildcards.norm,
        agg=lambda wildcards: wildcards.agg,
        qtl_trait=lambda wildcards: wildcards.qtl_trait,
        drug=lambda wildcards: wildcards.drug
    resources:
        mem_mb=4000,
        time="00:20:00"
    shell:
        """
        Rscript {input.script} \
            --input {input.processed_pheno} \
            --output {output.scan} \
            --normalization {params.norm} \
            --aggregation {params.agg} \
            --qtl_trait {params.qtl_trait} \
            --drug {params.drug}
        """

# --- Rule: Permutation Analysis ---
rule run_permutation:
    input:
        script="scripts/restructuring/permTest.R",
        scan="data/processed/ropscan/{norm}_{agg}_{qtl_trait}_{drug}.rds",
        pheno="data/processed/phenotypes/boxCoxTest/{norm}_{agg}_{drug}.csv"
    output:
        threshold="data/processed/scan_thresholds/{norm}_{agg}_{qtl_trait}_{drug}_threshold.rds",
        permuted_scan="data/processed/scan_thresholds/{norm}_{agg}_{qtl_trait}_{drug}_perm.rds"
    params:
        norm=lambda wildcards: wildcards.norm,
        agg=lambda wildcards: wildcards.agg,
        qtl_trait=lambda wildcards: wildcards.qtl_trait,
        drug=lambda wildcards: wildcards.drug
    resources:
        mem_mb=8000,
        time="08:00:00"
    shell:
        """
        Rscript {input.script} \
            --input_scan {input.scan} \
            --input_pheno {input.pheno} \
            --output_threshold {output.threshold} \
            --output_scan {output.permuted_scan} \
            --qtl_trait {params.qtl_trait} \
            --drug {params.drug}
        """